# Hey Emacs, this is a -*- python -*- file
#
# Copyright Â© 2011-2013 Jesse DeGuire
#
# This file is part of Projector Controller.
#
# Projector Controller is free software: you can redistribute it and/or 
# modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# Projector Controller is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of 
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU 
# General Public License for more details.
#
# You should have received a copy of the GNU General Public License along
# with Projector Controller.  If not, see <http://www.gnu.org/licenses/>.
#
# File:   SConstruct
# Author: Jesse DeGuire
#
# Main file for the SCons build system and sets up build variables
# common to both the bootloader and application firmware.
#

import os

#---------------
# Global project options
# These apply to both the bootloader and app
#---------------
avr_c   = 'avr-gcc'       # AVR GCC
avr_as  = 'avr-as'        # AVR Assembler
avr_oc  = 'avr-objcopy'   # AVR Object Copy
optlev  = 's'             # optimization level (0, 1, 2, 3, s)
fcy     = '20000000'      # oscillator frequency in Hertz
mcu     = 'atmega328p'    # microcontroller

cstd    = 'gnu99'         # C99 standard with GCC extensions
cdebug  = '-g'            # debug options


# Separate individual options with commas and enclose them all in brackets.

# warning options
cwarn   = ['-Wall']

# additional options to pass to GCC
copts   = ['-funsigned-bitfields', '-fpack-struct', '-fshort-enums']

# library search paths (do not prepend -L; SCons does that automatically)
clibp   = []

# libraries to include (do not prepend -l; SCons does that automatically, too)
clibs   = ['m']

# additional include directories (do not prepend -I; '.' and 'common/' already included)
cinc    = []

# additional preprocessor defines (do not prepend -D)
cdefs = []

# linker options (you may need to use '-Wl,option' to pass to linker)
lopts = ['-Wl,--relax']

#---------------
# End global project options
# App- and bootloader-specific options are in their respective SConscript files.
# --------------



# create a new scons build environment, importing the system's PATH
env = Environment(ENV = {'PATH' : os.environ['PATH']})


# location of common directory 
# The '#' symbol at beginning of the path makes it relative to this file rather than relative to 
# whatever SConscript file it is referenced in.
commonpath = '#common' + os.sep


# SCons tries to determine the appropriate compiler based on platform and what it finds.  We need to
# make sure that we use avr-gcc regardless of platform.
env.Replace(CC = avr_c)
env.Replace(AS = avr_as)
env.Replace(CPPDEFPREFIX = '-D')
env.Replace(CPPDEFSUFFIX = '')
env.Replace(INCPREFIX = '-I')
env.Replace(INCSUFFIX = '')
env.Replace(LIBDIRPREFIX = '-L')
env.Replace(LIBDIRSUFFIX = '')
env.Replace(LIBLINKPREFIX = '-l')
env.Replace(LIBLINKSUFFIX = '')
env.Replace(PROGSUFFIX = '.elf')        # avr-gcc creates elf files by default
env.Replace(OBJSUFFIX = '.o')

# feed in our options from the top of this file
env.Append(CCFLAGS = ['-O' + optlev, '-mmcu=' + mcu, '-std=' + cstd, cdebug])
env.Append(CCFLAGS = cwarn)
env.Append(CCFLAGS = copts)
env.Append(CPPPATH = ['.', commonpath])           # include local and common dirs by default
env.Append(CPPPATH = cinc)
env.Append(LIBS = clibs)
env.Append(LIBPATH = clibp)
env.Append(CPPDEFINES = 'F_CPU=' + fcy)    # standard macro for oscillator frequency
env.Append(CPPDEFINES = cdefs)
env.Append(LINKFLAGS = '-mmcu=' + mcu)
env.Append(LINKFLAGS = lopts)

# add our own variables to the environement
env['OBJCOPY'] = avr_oc
env['HEXSUFFIX'] = '.hex'
env['MCU'] = mcu
env['OUTDIR'] = 'output/'

# Export variables for use by the SConscript files and process the files.  The SConscript files
# contain the actual targets used for building.  Build output is set to go into the directory given
# by variant_dir and sources are not duplicated in the output directory.
env.Export(['env', 'commonpath'])
SConscript('app/SConscript', variant_dir = 'app/' + env['OUTDIR'], duplicate = 0)
SConscript('bootloader/SConscript', variant_dir = 'bootloader/' + env['OUTDIR'], duplicate = 0)

# printed with 'scons -h'
Help("""
TODO: Actually put something useful here...
""")
